# Windows build extras
target_sources(cschem PRIVATE tools/icons/source/cschem.rc)
target_sources(cpcb PRIVATE tools/icons/source/cpcb.rc)
target_link_libraries(cschem secur32.lib)
target_link_libraries(cpcb secur32.lib)


# Windows NSIS packaging
set(CPACK_GENERATOR "NSIS")
get_filename_component(_qt5_bin "${_qt5_install_prefix}/../../bin" ABSOLUTE)
find_program(WINDEPLOYQT_EXECUTABLE NAMES windeployqt HINTS "${_qt5_bin}" REQUIRED)

function(windeploy target)
  add_custom_command(TARGET ${target}
    POST_BUILD
    COMMAND "${WINDEPLOYQT_EXECUTABLE}" --dir windeploy --compiler-runtime ${CMAKE_CFG_INTDIR}/${target}.exe
	)
endfunction()

windeploy(cschem)
windeploy(cpcb)

set(CPACK_GENERATOR "NSIS")
install(TARGETS cschem)
install(TARGETS cpcb)
install(DIRECTORY ${CMAKE_BINARY_DIR}/windeploy/ DESTINATION bin)
install(DIRECTORY symbols DESTINATION cschem)
install(DIRECTORY pcb-outlines DESTINATION cpcb)

set(CPACK_RESOURCE_FILE_LICENSE "${CMAKE_SOURCE_DIR}/GPL-3.0.txt")
set(CPACK_PACKAGE_INSTALL_DIRECTORY "CSchem")
set(CPACK_NSIS_MUI_ICON "${CMAKE_SOURCE_DIR}/tools/icons/generated/cschem.ico")

set(CPACK_PACKAGE_EXECUTABLES
    cschem "CSchem"
	cpcb "CPCB")

set(CPACK_NSIS_EXTRA_PREINSTALL_COMMANDS
    "!include \\\"..\\\\..\\\\..\\\\..\\\\tools\\\\FileAssociation.nsh\\\"")

# Create association
set(CPACK_NSIS_EXTRA_INSTALL_COMMANDS "\\\${RegisterExtension} '$INSTDIR\\\\bin\\\\cschem.exe' '.cschem' 'cschem_schematic'\n\
\\\${RegisterExtension} '$INSTDIR\\\\bin\\\\cpcb.exe' '.cpcb' 'cpcb_layout'")
# my_program_key can be any string that gives some hint what this file type is about. And should not contain strings

# Remove association
set(CPACK_NSIS_EXTRA_UNINSTALL_COMMANDS "\\\${UnRegisterExtension} '.cschem' 'cschem_schematic'\n\
\\\${UnRegisterExtension} '.cpcb' 'cpcb_layout'")

#set(CPACK_NSIS_EXTRA_UNINSTALL_COMMANDS	

set(CPACK_NSIS_MUI_FINISHPAGE_RUN "cschem.exe")
